plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'me.deathrealms'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'matt-studios'
        url = 'https://repo.mattstudios.me/artifactory/public/'
    }
    maven {
        name = 'aikar'
        url = 'https://repo.aikar.co/content/groups/aikar/'
    }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    implementation 'dev.triumphteam:triumph-gui:3.1.2'
    implementation 'co.aikar:acf-bukkit:0.5.0-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.19.3-R0.1-SNAPSHOT'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.WARN
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'co.aikar.commands', 'me.deathrealms.inventoryhistory.shade.acf'
    relocate 'co.aikar.locales', 'me.deathrealms.inventoryhistory.shade.locales'
    relocate 'dev.triumphteam.gui', 'me.deathrealms.inventoryhistory.shade.gui'
    minimize()
    archiveFileName = "inventoryhistory-${version}.jar"
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
}

build.dependsOn shadowJar